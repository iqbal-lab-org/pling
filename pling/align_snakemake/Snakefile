import os
import pandas as pd
from pling.utils import get_pling_root_dir

configfile: "../config.yaml"

OUTPUTPATH = config["output_dir"]
PREFIX = config["prefix"]
FASTAFILES = [el[0] for el in pd.read_csv(config["genomes_list"], header=None).values]
FASTAEXT = {os.path.splitext(os.path.basename(el))[0]:os.path.splitext(os.path.basename(el))[1] for el in FASTAFILES}
GENOMES = list(FASTAEXT.keys())
FASTAPATH = os.path.dirname(FASTAFILES[0])

include: "../common_rules/common_rules.smk"

rule all:
    input:
        unimogs = get_files("unimog", OUTPUTPATH, get_pairs(GENOMES)),
        seq_jaccard = f"{OUTPUTPATH}/jaccard/all_pairs_jaccard_distance.tsv",
        communities = f"{OUTPUTPATH}/jaccard/jaccard_communities"

rule make_unimogs:
    input:
        genome_1_fasta=lambda wildcards: f"{FASTAPATH}/{wildcards.genome1}{FASTAEXT[wildcards.genome1]}",
        genome_2_fasta=lambda wildcards: f"{FASTAPATH}/{wildcards.genome2}{FASTAEXT[wildcards.genome2]}"
    output:
        unimogs=f"{OUTPUTPATH}/unimogs/{{genome1}}~{{genome2}}_align.unimog",
        jaccard_distance=f"{OUTPUTPATH}/tmp_files/jaccard_pairwise/{{genome1}}~{{genome2}}_jaccard_distance.tsv",
        seq_blocks=f"{OUTPUTPATH}/unimogs/{{genome1}}~{{genome2}}_map.txt"
    threads: config["make_unimogs_threads"]
    resources:
        mem_mb=lambda wildcards, attempt: config["make_unimogs_mem"]*attempt
    params:
        genome1 = lambda wildcards: wildcards.genome1,
        genome2 = lambda wildcards: wildcards.genome2,
        identity_threshold = config["identity_threshold"],
        pling_root_dir = get_pling_root_dir()
    conda: "../envs/integerise.yaml"
    shadow: "shallow"
    shell:
        """
        # set conda env first in $PATH - TODO: keep this or manage in other way?
        # this issue happens when running a pipeline and a conda env is already active but it is not the first
        # in the $PATH that have python installed (let's call this env E).
        # In this case, snakemake will activate the "../envs/integerise.yaml" env but the python in the $PATH is the one
        # in E, which won't have the dependencies required to run the script below.
        # This is the expected behaviour, is not a bug in conda or snakemake.
        # For more details, see https://github.com/conda/conda/issues/9392#issuecomment-1291041085 
        PATH="$CONDA_PREFIX"/bin:$PATH  # quick fix for the issue described above
        python {params.pling_root_dir}/pling/align_snakemake/unimog.py \
            --genome_1_fasta {input.genome_1_fasta} \
            --genome1 {params.genome1} \
            --genome_2_fasta {input.genome_2_fasta} \
            --genome2 {params.genome2} \
            --identity_threshold {params.identity_threshold} \
            --unimogs {output.unimogs} \
            --jaccard {output.jaccard_distance} \
            --seq_blocks {output.seq_blocks}
        """

rule get_communities:
    input:
        jaccard = f"{OUTPUTPATH}/jaccard/all_pairs_jaccard_distance.tsv",
        genomes = rules.create_genomes_tsv.output.genomes_tsv
    output:
        communities = directory(f"{OUTPUTPATH}/jaccard/jaccard_communities"),
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: config["get_communities_mem"]*attempt
    conda: "../envs/plasnet.yaml"
    params:
        jaccard_distance=config["seq_jaccard_distance"],
        bh_connectivity=config["bh_connectivity"],
        bh_neighbours_edge_density=config["bh_neighbours_edge_density"]
    shell: """
        plasnet split \
            --distance-threshold {params.jaccard_distance} \
            --bh-connectivity {params.bh_connectivity} \
            --bh-neighbours-edge-density {params.bh_neighbours_edge_density} \
            --output-plasmid-graph \
            {input.genomes} \
            {input.jaccard} \
            {output.communities}
    """
